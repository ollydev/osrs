{$DEFINE INCLUDED_MAINSCREEN}
{$include_once osrs.simba}

type
  TRSMainScreen = record
    Bounds: TBox;
    UptextFilter: TOCRFilter;
  end;

procedure TRSMainScreen.Setup;
begin
  case RSClient.ClientMode() of
    ERSClientMode.FIXED:
      begin
        Self.Bounds := TBox.Create(4,4,515,337);
      end;

    ERSClientMode.RESIZABLE_CLASSIC,
    ERSClientMode.RESIZABLE_MODERN:
      begin
        Self.Bounds := Target.Bounds;
      end;
  end;

  Self.UptextFilter := TOCRAnyColorFilter.Create(85, 60);
  Self.UptextFilter.Blacklist := ',.'+#39+#34;
end;

function TRSMainScreen.GetUpText: String;
begin
  if (RSClient.ClientMode = ERSClientMode.FIXED) then
    Result := OCR.RecognizeStatic([7, 7, 500, 25], Self.UptextFilter, FONT_BOLD_12_SHADOW)
  else
    Result := OCR.RecognizeStatic([3, 3, 500, 21], Self.UptextFilter, FONT_BOLD_12_SHADOW);
end;

function TRSMainScreen.IsUpText(text: String; similarity: Single = 0.9): Boolean;
var
  upText: String;
begin
  upText := Copy(Self.GetUpText(), 1, Length(text));
  Result := text.Similarity(upText) >= similarity;
end;

function TRSMainScreen.IsUpText(strings: TStringArray; similarity: Single = 0.9): Boolean; overload;
var
  upText, str, tmp: String;
begin
  upText := Self.GetUpText();

  for str in strings do
  begin
    tmp := Copy(upText, 1, Length(str));
    if str.Similarity(tmp) >= similarity then
      Exit(True);
  end;
end;

var
  MainScreen: TRSMainScreen;
