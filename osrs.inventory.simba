{$DEFINE INCLUDED_RSINVENTORY}
{$include_once osrs.simba}

type
  TRSInventory = record
    Bounds: TBox;
    Slots: TBoxArray;
  end;

function TRSInventory.IsOpen: Boolean;
begin
  Result := RSGameTabs.IsCurrentTab(ERSGameTab.INVENTORY);
end;

function TRSInventory.Open: Boolean;
begin
  Result := Self.IsOpen() or RSGameTabs.Open(ERSGameTab.INVENTORY);
end;

function TRSInventory.MouseSlot(Index: Integer): Boolean;
begin
  Target.MouseMove(Self.Slots[Index]);
end;

function TRSInventory.IsSlotSelected(Index: Integer): Boolean;
var
  B: TBox;
begin
  B := Self.Slots[Index];
  B.Y1 += 8; // Skip (possible) item text

  Result := IsItemSelected(B);
end;

function TRSInventory.GetSelectedSlot: Integer;
begin
  for Result := 0 to 27 do
    if Self.IsSlotSelected(Result) then
      Exit;

  Result := -1;
end;

function TRSInventory.IsEmpty: Boolean;
begin
  Result := not IsItemIn(Self.Slots.Merge());
end;

function TRSInventory.IsFull: Boolean;
begin
  Result := Length(Self.GetUsedSlots()) = 28;
end;

function TRSInventory.GetUsedSlots: TIntegerArray;
var
  TPA: TPointArray;
  ATPA: T2DPointArray;
  I, Count: Integer;
begin
  TPA := FindItemBorder(Self.Slots.Merge()) + Self.Slots.Centers();
  if (Length(TPA) <= Length(Self.Slots)) then
    Exit;
  ATPA := TPA.PartitionEx([Self.Slots[0].X1, Self.Slots[0].Y1], 40, 36);

  SetLength(Result, Length(Self.Slots));
  for I := 0 to High(ATPA) do
    if (Length(ATPA[I]) > 1) then
    begin
      Result[Count] := I;
      Inc(Count);
    end;
  SetLength(Result, Count);
end;

function TRSInventory.CountUsedSlots: Integer;
begin
  Result := Length(Self.GetUsedSlots());
end;

function TRSInventory.FindItems(ItemName: String): TIntegerArray;
begin
  if not Self.Open() then
    Exit;

  Result := RSInterfaceUtils.ItemFind(ItemName, Self.Slots);
end;

function TRSInventory.FindAllItems(ItemNames: TStringArray): TIntegerArray;
begin
  if not Self.Open() then
    Exit;

  Result := RSInterfaceUtils.ItemFind(ItemNames, Self.Slots);
end;

function TRSInventory.FindItem(ItemName: String): Integer;
var
  Arr: TIntegerArray;
begin
  Arr := RSInterfaceUtils.ItemFind(ItemName, Self.Slots, 1);
  if Length(Arr) then
    Result := Arr[0]
  else
    Result := -1;
end;

function TRSInventory.MouseItem(ItemName: String): Boolean;
var
  Index: Integer;
begin
  Index := Self.FindItem(ItemName);

  Result := Index > -1;
  if Result then
    Self.MouseSlot(Index);
end;

function TRSInventory.ClickItem(ItemName: String; Button: EMouseButton): Boolean;
begin
  if not Self.Open() then
    Exit;

  Result := RSInterfaceUtils.ItemClick(ItemName, Self.Slots, Button);
end;

function TRSInventory.ClickAnyItem(ItemNames: TStringArray; Button: EMouseButton): Boolean;
begin
  if not Self.Open() then
    Exit;

  Result := RSInterfaceUtils.ItemClickAny(ItemNames, Self.Slots, Button);
end;

function TRSInventory.CountItem(ItemName: String): Integer;
begin
  Result := Length(Self.FindItems(ItemName));
end;

function TRSInventory.CountAllItems(ItemNames: TStringArray): Integer;
begin
  Result := Length(Self.FindAllItems(ItemNames));
end;

function TRSInventory.CountItemStack(ItemName: String): Integer;
var
  Index: Integer;
begin
  Index := Self.FindItem(ItemName);
  if (Index > -1) then
    Result := CountItemStack(Self.Slots[Index]);
end;

function TRSInventory.HasItem(ItemName: String): Boolean;
begin
  Result := Self.FindItem(ItemName) > -1;
end;

function TRSInventory.HasAnyItems(ItemNames: TStringArray): Boolean;
var
  ItemName: String;
begin
  for ItemName in ItemNames do
    if Self.FindItem(ItemName) > -1 then
      Exit(True);
end;

procedure TRSInventory.Setup;
begin
  Self.Bounds := RSGameTabs.TabBounds;
  Self.Slots := TBoxArray.Create([Self.Bounds.X1 + 16, Self.Bounds.Y1 + 9], 4, 7, 35, 31, [7, 5]);
end;

var
  RSInventory: TRSInventory;
